* -*- LaGriT -*-
*
* ----------------------------------------------------------------------
*
*                           Brad T. Aagaard
*                        U.S. Geological Survey
*
* <LicenseText>
*
* ----------------------------------------------------------------------
*
* Mesh for simple dynamic benchmark with a kinematic fault rupture.
*
* Fault is 30km long and 15km wide
* Fault runs north-south (y direction)
*
* Domain is 72.0 km by 36.0 km by 36.0 km
*   x: -18e+3 to +18e+3
*   y: -36e+3 to +36e+3
*   z: -36e+3 to 0
*

cmo / create / halfspace
cmo / setatt / halfspace / ipolydat / no

* ----------------------------------------------------------------------
* CREATE GEOMETRY
* ----------------------------------------------------------------------
** Boundaries of mesh
define / domain_xMin / -18.0e+3
define / domain_xMax /  18.0e+3
define / domain_yMin / -36.0e+3
define / domain_yMax /  36.0e+3
define / domain_zMin / -36.0e+3
define / domain_zMax /   0.0e+3


** Create boundary surfaces
** Convention is that normals for boundary surfaces point out of domain.

surface / surf_xMin / reflect / plane / &
  domain_xMin  domain_yMin  domain_zMax / &
  domain_xMin  domain_yMax  domain_zMax / &
  domain_xMin  domain_yMin  domain_zMin
surface / surf_yMin / reflect / plane / &
  domain_xMax  domain_yMin  domain_zMax / &
  domain_xMin  domain_yMin  domain_zMax / &
  domain_xMax  domain_yMin  domain_zMin
surface / surf_xMax / reflect / plane / &
  domain_xMax  domain_yMax  domain_zMax / &
  domain_xMax  domain_yMin  domain_zMax / &
  domain_xMax  domain_yMax  domain_zMin
surface / surf_yMax / reflect / plane / &
  domain_xMin  domain_yMax  domain_zMax / &
  domain_xMax  domain_yMax  domain_zMax / &
  domain_xMin  domain_yMax  domain_zMin
surface / surf_zMin / reflect / plane / &
     0.0e+0    0.0e+0  domain_zMin / &
   -10.0e+3    0.0e+0  domain_zMin / &
     0.0e+0   10.0e+3  domain_zMin
surface / surf_zMax / reflect / plane / &
     0.0e+0    0.0e+0    domain_zMax / &
     0.0e+0   10.0e+3    domain_zMax / &
   -10.0e+3    0.0e+0    domain_zMax

** Create fault plane.
surface / surf_fault / intrface / plane / &
  0.0   0.0  0.0 / &
  0.0   1.0  0.0 / &
  0.0   0.0  1.0 /

** Create regions
** positive
region / r_positive / &
  le surf_xMax and ge surf_fault  &
  and le surf_yMax and le surf_yMin  &
  and le surf_zMax and le surf_zMin /
** negative
region / r_negative / &
  le surf_xMin and lt surf_fault  &
  and le surf_yMax and le surf_yMin  &
  and le surf_zMax and le surf_zMin /

** Create material regions
** positive
mregion / m_positive / &
  le surf_xMax and gt surf_fault  &
  and le surf_yMax and le surf_yMin  &
  and le surf_zMax and le surf_zMin /
** negative
mregion / m_negative / &
  le surf_xMin and lt surf_fault  &
  and le surf_yMax and le surf_yMin  &
  and le surf_zMax and le surf_zMin /


* ----------------------------------------------------------------------
* CREATE MESH
* ----------------------------------------------------------------------
define / nx / 49
define / ny / 97
define / nz / 49

createpts / xyz / nx,ny,nz / &
  domain_xMin  domain_yMin  domain_zMin / &
  domain_xMax  domain_yMax  domain_zMax / &
  1,1,1

setpts
connect / noadd

settets / geometry
resetpts / cell_color

cmo / printatt / halfspace / imt1 / minmax /
cmo / printatt / halfspace / itetclr / minmax /
quality / aspect / y


* ----------------------------------------------------------------------
* EXPORT MESH
* ----------------------------------------------------------------------
** Remove child points
resetpts / parent
rmpoint / compress

** Identify groups
pset / pts_tmp / surface / surf_fault /
* Extract points -15.0e+3m < y < 15.2e+3m, -12.990e+3m < z < 0m
pset / elemfaces_fault / geom / xyz / pset,get,pts_tmp / &
  -99.0e+3  -15.001e+3  -15.001e+3 / &
  +99.0e+3   15.001e+3    0.001e+3 /
pset / pts_tmp / delete

* Identify nodes on boundaries
pset / elemfaces_xmin / surface / surf_xMin /
pset / elemfaces_xmax / surface / surf_xMax /
pset / elemfaces_ymin / surface / surf_yMin /
pset / elemfaces_ymax / surface / surf_yMax /
pset / elemfaces_zmin / surface / surf_zMin /
pset / elemfaces_zmax / surface / surf_zMax /

* Merge materials
eltset / elastic1 / itetclr / eq / 1
eltset / elastic2 / itetclr / eq / 2
eltset / elastic / union / elastic1 elastic2
cmo / setatt / halfspace / itetclr / eltset,get,elastic / 1
eltset / elastic1 / delete
eltset / elastic2 / delete
eltset / elastic / delete

** Write mesh.
dump / gmv / halfspace_tet4_lagrit.gmv // binary
pset / -all- / write / halfspace_tet4_lagrit.pset / ascii

* ----------------------------------------------------------------------
finish


* End of file
