## /Applications/Plotting/Cubit_10_2.app/Contents/MacOS/Cubit_10_2
## Cubit Version 10.2
## Cubit Build 8
## Revised 10/17/2006 15:13:41 MST
## Running 06/27/2008 11:07:35 AM
## Command Options:
## -warning = On
## -information = On

# ----------------------------------------------------------------------
# Create outer block.
# ----------------------------------------------------------------------

# Block is 200 km x 200 km x 50 km
# -100 km <= x <= 100 km
# -100 km <= y <= 100 km
# -50 km <= z <= 0 km
reset
brick x 200000 y 200000 z 50000

# Translate block so the top is at z=0.
volume 1 move x 0 y 0 z -25000

# ----------------------------------------------------------------------
# Create inner block that will be at constant resolution.
# ----------------------------------------------------------------------

# Block is 50 km x 50 km x 25 km
# -25 km <= x <= 25 km
# -25 km <= y <= 25 km
# -25 km <= y <= 0 km
brick x 50000 y 50000 z 25000

# Translate block so the top is at z=0.
volume 2 move x 0 y 0 z -12500

# ----------------------------------------------------------------------
# Create interface surfaces.
# ----------------------------------------------------------------------
create planar surface with plane xplane offset 0
surface 13 name "dike_surface"
create planar surface with plane zplane offset -10000
surface 14 name "material_interface"
create planar surface with plane yplane offset 0
surface 15 name "symmetry_plane"

# ----------------------------------------------------------------------
# Chop outer volume with inner volume and then cut both with interfaces.
# ----------------------------------------------------------------------
chop volume 1 with volume 2
compress all
webcut volume all with plane dike_surface
webcut volume all with plane symmetry_plane
webcut volume all with plane material_interface

# ----------------------------------------------------------------------
# Imprint all volumes, merging surfaces.
# ----------------------------------------------------------------------
imprint all with volume all
merge all

# ----------------------------------------------------------------------
# Remove surface bodies.
# ----------------------------------------------------------------------
delete body 1 2 3 6 7 10 11 14 17 18 21 24

# ----------------------------------------------------------------------
# Create initial mesh at constant resolution.
# ----------------------------------------------------------------------
volume all size 6000
mesh volume all

# ----------------------------------------------------------------------
# Refine region inside inner box.
# ----------------------------------------------------------------------
group "inner_box" add hex with x_coord > -25000 and with x_coord < 25000 and with y_coord > -25000 and with y_coord < 25000 and with z_coord > -25000
refine hex in inner_box depth 0

# ----------------------------------------------------------------------
# Create blocks for materials.
# ----------------------------------------------------------------------
block 1 volume 4 5 8 9 12 13 15 16
block 1 name "elastic"
block 2 volume 19 20 22 23 25 26 27 28
block 2 name "viscoelastic"

# ----------------------------------------------------------------------
# Create fault nodeset.
# ----------------------------------------------------------------------
group "dike" add node in dike_surface@A
group "dike" add node in dike_surface@B
group "dike" add node in dike_surface@D
group "dike" add node in dike_surface@G
nodeset 10 dike

# ----------------------------------------------------------------------
# Create nodeset for +x face.
# ----------------------------------------------------------------------
group "face_xpos" add node in surface 121 176 131 179
nodeset 11 face_xpos
nodeset 11 name "face_xpos"

# ----------------------------------------------------------------------
# Create nodeset for -x face.
# ----------------------------------------------------------------------
group "face_xneg" add node in surface 150 197 153 208
nodeset 12 face_xneg
nodeset 12 name "face_xneg"

# ----------------------------------------------------------------------
# Create nodeset for +y face.
# ----------------------------------------------------------------------
group "face_ypos" add node in surface 122 151 132 154
nodeset 13 face_ypos
nodeset 13 name "face_ypos"

# ----------------------------------------------------------------------
# Create nodeset for -y face.
# ----------------------------------------------------------------------
group "face_yneg" add node in surface 177 198 180 207
nodeset 14 face_yneg
nodeset 14 name "face_yneg"

# ----------------------------------------------------------------------
# Create nodeset for -z face.
# ----------------------------------------------------------------------
group "face_zneg" add node in surface 71 93 74 103
nodeset 15 face_zneg
nodeset 15 name "face_zneg"

# ----------------------------------------------------------------------
# Create nodeset for +z face.
# ----------------------------------------------------------------------
group "face_zpos" add node in surface 59 68 83 95 61 78 90 101
nodeset 16 face_zpos
nodeset 16 name "face_zpos"

# ----------------------------------------------------------------------
# Export exodus file.
# ----------------------------------------------------------------------
export mesh "dike-disloc_hex8_graded_2000m.exo" dimension 3 overwrite
