# Description:
# This journal uses the geometry from file geometry.jou to create a mesh
# with dimensions of 2000 km (x), 1000 km (y), and 400 km (z).  The mesh is
# divided into 2 horizontal layers:
# a surface layer from 0 to 40 km, marking elastic layer;
# a bottom layer from 40 to 400 km, which approximates the viscoelastic
# half-space.
# The fault plane is also cut horizontally at 20 km depth, representing
# the division between the upper locked section and the lower creeping
# section.
#
# The mesh is cut vertically by the fault surface, which lies along the
# x-axis, and by a symmetry plane, which lies along the y-axis.
# The fault is restricted to the upper (elastic) layer.
#
# The volume is meshed at an initial resolution of 20 km.
# An inner volume with dimensions of 480 km (x), 240 km (y), and 100 km (z)
# is then put through 1 level of refinement.
# The final mesh contains 155061 vertices and 145584 cells.
# ----------------------------------------------------------------------
# Generate geometry
# ----------------------------------------------------------------------
playback 'geometry.jou'

# ----------------------------------------------------------------------
# Set interval size for outer surfaces to 20 km and generate initial mesh
# ----------------------------------------------------------------------
volume all size 20000.0
mesh volume all

# ----------------------------------------------------------------------
# Refine region inside inner box.
# ----------------------------------------------------------------------
group "inner_box" add hex with x_coord > -240000.0 and with x_coord < 240000.0 and with y_coord > -120000.0 and with y_coord < 120000.0 and with z_coord > -100000.0
refine hex in inner_box depth 0

# ----------------------------------------------------------------------
# Smooth mesh.
# ----------------------------------------------------------------------
set debug 91 on
volume all smooth scheme condition number
smooth volume all

# ----------------------------------------------------------------------
# Create blocks for materials
# ----------------------------------------------------------------------
block 1 volume 1 6 15 19
block 1 name "elastic"
block 2 volume 10 13 22 24
block 2 name "viscoelastic"

# ----------------------------------------------------------------------
# Create fault nodeset
# ----------------------------------------------------------------------
group "fault" add node in fault_surface
group "fault" add node in fault_surface@A
group "fault" add node in fault_surface@D
group "fault" add node in fault_surface@E
nodeset 10 fault
nodeset 10 name "fault"

# ----------------------------------------------------------------------
# Create nodeset for +x face
# ----------------------------------------------------------------------
group "face_xpos" add node in surface 61 55 89 87
nodeset 11 face_xpos
nodeset 11 name "face_xpos"

# ----------------------------------------------------------------------
# Create nodeset for -x face
# ----------------------------------------------------------------------
group "face_xneg" add node in surface 73 75 97 104
nodeset 12 face_xneg
nodeset 12 name "face_xneg"

# ----------------------------------------------------------------------
# Create nodeset for +y face
# ----------------------------------------------------------------------
group "face_ypos" add node in surface 29 45 35 47
nodeset 13 face_ypos
nodeset 13 name "face_ypos"

# ----------------------------------------------------------------------
# Create nodeset for -y face
# ----------------------------------------------------------------------
group "face_yneg" add node in surface 43 31 49 33
nodeset 14 face_yneg
nodeset 14 name "face_yneg"

# ----------------------------------------------------------------------
# Create nodeset for -z face
# ----------------------------------------------------------------------
group "face_zneg" add node in surface 103 92 96 84
nodeset 15 face_zneg
nodeset 15 name "face_zneg"

# ----------------------------------------------------------------------
# Create nodeset for +z face
# ----------------------------------------------------------------------
group "face_zpos" add node in surface 78 62 70 54
nodeset 17 face_zpos
nodeset 17 name "face_zpos"

# ----------------------------------------------------------------------
# Create nodeset for -y face without fault or x-boundaries
# ----------------------------------------------------------------------
group "face_yneg_nofault" add node in face_yneg
group "face_yneg_nofault" remove node in fault
group "face_yneg_nofault" remove node in face_xpos
group "face_yneg_nofault" remove node in face_xneg
nodeset 18 face_yneg_nofault
nodeset 18 name "face_yneg_nofault"

# ----------------------------------------------------------------------
# Create nodeset for +y face without fault or x-boundaries
# ----------------------------------------------------------------------
group "face_ypos_nofault" add node in face_ypos
group "face_ypos_nofault" remove node in fault
group "face_ypos_nofault" remove node in face_xpos
group "face_ypos_nofault" remove node in face_xneg
nodeset 19 face_ypos_nofault
nodeset 19 name "face_ypos_nofault"

# ----------------------------------------------------------------------
# Export exodus file
# ----------------------------------------------------------------------
export mesh "hex8_refined_20km.exo" dimension 3 overwrite
